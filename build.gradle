import org.gradle.api.tasks.testing.logging.TestLogEvent
import groovy.json.JsonSlurper

plugins {
    id 'idea'
    id 'java'
    id 'se.thinkcode.cucumber-runner' version '0.0.11'
    id "com.github.spacialcircumstances.gradle-cucumber-reporting" version "0.1.24"
    id 'io.qameta.allure' version '2.10.0'
}

group 'fifernandez.test'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.2"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.2"
    testImplementation "io.cucumber:cucumber-java:7.3.2"
    testImplementation "io.cucumber:cucumber-junit:7.3.2"
    testImplementation "org.junit.vintage:junit-vintage-engine:5.8.2"
    testImplementation "org.json:json:20220320"
    testImplementation "com.googlecode.json-simple:json-simple:1.1.1"
    testImplementation "io.rest-assured:rest-assured:5.1.0"
    testImplementation "commons-io:commons-io:2.11.0"
    testImplementation "io.qameta.allure:allure-cucumber7-jvm:2.18.1"
    testImplementation "org.slf4j:slf4j-simple:1.7.30"
}

test {
    systemProperty("cucumber.junit-platform.naming-strategy", "long")
    systemProperty("cucumber.filter.tags", getTags())
    systemProperty("allure.results.directory", "build/allure-results")
    testLogging {
        events TestLogEvent.FAILED, TestLogEvent.PASSED, TestLogEvent.SKIPPED
    }
    systemProperties(project.gradle.startParameter.systemPropertiesArgs)
    useJUnitPlatform()
}

allure {
    version = '2.8.1'
    aspectjweaver = true
    autoconfigure = true
    /*report {
        reportDir.set(project.reporting.baseDirectory)
    }*/
}

cucumberReports {
    outputDir = file('build/reports/')
    buildId = '0'
    reports = files('build/reports/cucumber.json')
    excludeTags = ["@TestCaseID:(|\\w+)", "@dev", "@qa", "@stage", "@prod"]
    expandAllSteps = false
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

cucumber {
    main = 'io.cucumber.core.cli.Main'
    tags = getTags()
    plugin = [
            "pretty",
            "json:build/reports/cucumber.json",
            "junit:build/reports/cucumber.xml",
            "html:build/reports/cucumber-report.html",
            "rerun:build/reports/rerun.txt",
            "io.qameta.allure.cucumber7jvm.AllureCucumber7Jvm"
    ]
}

tasks.cucumber {
    finalizedBy generateCucumberReports
    System.setProperty("allure.results.directory", "build/allure-results")
}

task runTests() {
    dependsOn assemble, testClasses
    finalizedBy generateCucumberReports
    doLast {
        javaexec {
            mainClass = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            if (System.getProperty("env") != null) {
                systemProperty "env", System.getProperty("env")
            }
            if (System.getProperty("mode") != null) {
                systemProperty "mode", System.getProperty("mode")
            }
            systemProperty "cucumber.filter.tags", getTags()
            systemProperty "allure.results.directory", "build/allure-results"
            args = [
                    '--plugin', 'pretty',
                    '--plugin', "json:${reporting.baseDir}/cucumber.json",
                    '--plugin', "junit:${reporting.baseDir}/cucumber.xml",
                    '--plugin', "html:${reporting.baseDir}/cucumber-report.html",
                    '--plugin', "rerun:${reporting.baseDir}/rerun.txt",
                    '--plugin', "io.qameta.allure.cucumber7jvm.AllureCucumber7Jvm",
                    '--glue', "src/test/resources/features",
                    '--glue', "support"
            ]
        }
    }
}

String getTags() {
    def jsonFile = file("src/test/java/config/config.json")
    def parsedJson = new JsonSlurper().parseText(jsonFile.text)
    String defaultEnvironment = parsedJson.defaultEnvironment
    List<String> environments = parsedJson.environments
    String selectedEnv = System.getProperty("env")
    String tags = ""
    if (selectedEnv == null) {
        tags = "@${defaultEnvironment}"
    } else {
        selectedEnv = selectedEnv.toLowerCase()
        if (environments.contains(selectedEnv)) {
            tags = "@${selectedEnv}"
        } else {
            tags = "@${defaultEnvironment}"
        }
    }
    tags += " and not @disable"
    if (System.getProperty("tags") != null) {
        tags = "(" + tags + ") and (" + System.getProperty("tags") + ")"
    }
    return tags
}
